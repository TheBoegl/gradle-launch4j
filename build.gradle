

/*
 * Copyright (c) 2019 Sebastian Boegl
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

plugins {
    id "com.gradle.plugin-publish" version "0.12.0"
    id 'java-gradle-plugin'
    id 'groovy'
    id 'jacoco'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.5"
    id "com.jfrog.artifactory" version "4.17.2"
    id 'org.unbroken-dome.test-sets' version '3.0.1'
}

// Write the plugin's classpath to a file to share with the tests.
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

repositories {
    jcenter()
}

ext {
    ARTIFACT_VERSION = '3.14'
    spock_version = '1.2-groovy-2.5'
}


testSets {
    backwardsCompatibiltyTest
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation ("net.sf.launch4j:launch4j:${ARTIFACT_VERSION}:core") {
        exclude(group: 'org.apache.ant', module: '*')
        exclude(group: 'net.java.abeille', module: '*')
        exclude(group: 'com.jgoodies', module: '*')
        exclude(group: 'foxtrot', module: '*')
    }

    testImplementation gradleTestKit()
    testImplementation ("org.spockframework:spock-core:${spock_version}") {
        exclude module: 'junit-dep'
        exclude module: 'groovy-all'
    }
    backwardsCompatibiltyTestImplementation sourceSets.test.output

    // Add the classpath file to the test runtime classpath.
    testRuntimeOnly files(createClasspathManifest)
}

group "edu.sc.seis.launch4j"
// use semantic versioning (http://semver.org/)
version "2.5.0"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

gradlePlugin {
    plugins {
        launch4j {
            id = 'edu.sc.seis.launch4j'
            displayName = 'launch4j gradle plugin'
            description = 'A gradle-plugin to create windows executables with launch4j for Java application.'
            implementationClass = 'edu.sc.seis.launch4j.Launch4jPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/TheBoegl/gradle-launch4j'
    vcsUrl = 'https://github.com/TheBoegl/gradle-launch4j'
    description = 'A gradle-plugin to create windows executables with launch4j for Java application.'
    tags = ['launch4j', 'executable', 'wrapper']

    plugins {
        launch4j {
            displayName = 'launch4j gradle plugin'
        }
    }
}

bintray {
    pkg {
        repo = 'gradle-plugins'
        version {
            vcsTag = "v${project.version}"
        }
    }
}

publishPlugins.dependsOn build

if (!project.hasProperty("artifactory_ojo_user")) {
    ext.artifactory_ojo_user = "user"
    ext.artifactory_ojo_password = "password"
}

artifactory {
    contextUrl = "https://oss.jfrog.org/artifactory"
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = "${artifactory_ojo_user}"
            password = "${artifactory_ojo_password}"
        }
        defaults {
            publications 'plugin'
        }
    }
}
//createReleaseTag.dependsOn publish

wrapper {
    gradleVersion = '6.8.3'
    distributionType = Wrapper.DistributionType.ALL
}

tasks.withType(Test) {
    systemProperty 'org.gradle.testkit.debug', System.getProperty('org.gradle.testkit.debug', 'false')
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }
}

check.dependsOn jacocoTestReport
