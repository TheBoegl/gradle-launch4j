plugins {
    id "com.gradle.plugin-publish" version "0.20.0"
    id 'java-gradle-plugin'
    id 'groovy'
    id 'jacoco'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id "com.jfrog.artifactory" version "4.26.3"
    id 'org.unbroken-dome.test-sets' version '4.0.0'
}

// Write the plugin's classpath to a file to share with the tests.
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files(sourceSets.main.runtimeClasspath)
        .withPropertyName("runtimeClasspath")
        .withNormalizer(ClasspathNormalizer)
    outputs.dir(outputDir)
        .withPropertyName("outputDir")

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

repositories {
    mavenCentral()
}

ext {
    ARTIFACT_VERSION = '3.14'
}

testSets {
    backwardsCompatibiltyTest
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation ("net.sf.launch4j:launch4j:${ARTIFACT_VERSION}:core") {
        exclude(group: 'org.apache.ant', module: '*')
        exclude(group: 'net.java.abeille', module: '*')
        exclude(group: 'com.jgoodies', module: '*')
        exclude(group: 'foxtrot', module: '*')
        exclude(group: 'com.thoughtworks.xstream')
    }
    implementation 'com.thoughtworks.xstream:xstream:1.4.18'

    testImplementation gradleTestKit()
    testImplementation platform("org.spockframework:spock-bom:2.0-groovy-3.0") {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation 'org.spockframework:spock-core'
    testImplementation 'org.spockframework:spock-junit4'
    backwardsCompatibiltyTestImplementation sourceSets.test.output

    // Add the classpath file to the test runtime classpath.
    testRuntimeOnly files(createClasspathManifest)
}

group "edu.sc.seis.launch4j"
// use semantic versioning (http://semver.org/)
version "2.5.1"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

gradlePlugin {
    plugins {
        launch4j {
            id = 'edu.sc.seis.launch4j'
            displayName = 'launch4j gradle plugin'
            description = 'A gradle-plugin to create windows executables with launch4j for Java application.'
            implementationClass = 'edu.sc.seis.launch4j.Launch4jPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/TheBoegl/gradle-launch4j'
    vcsUrl = 'https://github.com/TheBoegl/gradle-launch4j'
    description = 'A gradle-plugin to create windows executables with launch4j for Java application.'
    tags = ['launch4j', 'executable', 'wrapper']

    plugins {
        launch4j {
            displayName = 'launch4j gradle plugin'
        }
    }
}

publishPlugins.dependsOn build

if (!project.hasProperty("artifactory_ojo_user")) {
    ext.artifactory_ojo_user = "user"
    ext.artifactory_ojo_password = "password"
}

artifactory {
    contextUrl = "https://oss.jfrog.org/artifactory"
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = "${artifactory_ojo_user}"
            password = "${artifactory_ojo_password}"
        }
        defaults {
            publications 'plugin'
        }
    }
}
//createReleaseTag.dependsOn publish

wrapper {
    gradleVersion = '7.3.3'
    distributionType = Wrapper.DistributionType.ALL
}

tasks.withType(Test) {
    systemProperty 'org.gradle.testkit.debug', System.getProperty('org.gradle.testkit.debug', 'false')
}
tasks.withType(Test).configureEach {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}
