plugins {
    id "com.gradle.plugin-publish" version "1.3.1"
    id 'java-gradle-plugin'
    id 'groovy'
    id 'jacoco'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id "com.jfrog.artifactory" version "4.33.1"
    id 'org.unbroken-dome.test-sets' version '4.1.0'
}

repositories {
    mavenCentral()
}

ext {
    ARTIFACT_VERSION = '3.50'
    spock_version = '2.3-groovy-3.0'
}

testSets {
    backwardsCompatibiltyTest
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation ("net.sf.launch4j:launch4j:${ARTIFACT_VERSION}:core") {
        exclude(group: 'org.apache.ant', module: '*')
        exclude(group: 'net.java.abeille', module: '*')
        exclude(group: 'com.jgoodies', module: '*')
        exclude(group: 'com.formdev', module: '*')
        exclude(group: 'commons-beanutils', module: '*')
        exclude(group: 'commons-logging', module: '*')
        exclude(group: 'com.thoughtworks.xstream')
    }
    implementation 'com.thoughtworks.xstream:xstream:1.4.21'

    testImplementation gradleTestKit()
    testImplementation ("org.spockframework:spock-core:${spock_version}") {
        exclude group: 'org.codehaus.groovy'
        exclude group: 'junit'
    }
    testImplementation 'junit:junit:4.13.2'
    backwardsCompatibiltyTestImplementation sourceSets.test.output
}

group "edu.sc.seis.launch4j"
// use semantic versioning (http://semver.org/)
version "3.0.7-SNAPSHOT"
if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_11)) {
    java.targetCompatibility = JavaVersion.VERSION_11
} else {
    java.toolchain.languageVersion = JavaLanguageVersion.of(11)
}
gradlePlugin {
    plugins {
        launch4j {
            id = 'edu.sc.seis.launch4j'
            displayName = 'launch4j gradle plugin'
            description = 'A gradle-plugin to create windows executables with launch4j for Java application.'
            implementationClass = 'edu.sc.seis.launch4j.Launch4jPlugin'
            tags = ['launch4j', 'executable', 'wrapper']
        }
    }

    website = 'https://github.com/TheBoegl/gradle-launch4j'
    vcsUrl = 'https://github.com/TheBoegl/gradle-launch4j'
    description = 'A gradle-plugin to create windows executables with launch4j for Java application.'

}

publishPlugins.dependsOn build

if (!project.hasProperty("artifactory_platform_user")) {
    ext.artifactory_platform_user = "user"
    ext.artifactory_platform_password = "password"
}

artifactory {
    contextUrl = "https://boegl.jfrog.io/artifactory"
    publish {
        repository {
            repoKey = 'snapshots-gradle-dev-local'
            username = "${artifactory_platform_user}"
            password = "${artifactory_platform_password}"
            maven = true
        }

        defaults {
            publications("pluginMaven")
            publishArtifacts = true
            publishPom = true
        }
    }
}
//createReleaseTag.dependsOn publish

wrapper {
    gradleVersion = '8.14.3'
    distributionType = Wrapper.DistributionType.ALL
}

tasks.withType(Test).configureEach {
    useJUnit()
    systemProperty 'org.gradle.testkit.debug', System.getProperty('org.gradle.testkit.debug', 'false')
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.required = true
    }
}

check.dependsOn jacocoTestReport
